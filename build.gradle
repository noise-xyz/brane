ext {
    jacksonVersion = '2.17.1'
    bouncyCastleVersion = '1.70'
    slf4jVersion = '2.0.13'
}

allprojects {
    group = 'com.github.noise-xyz.brane'
    version = '0.1.0-alpha'

    repositories {
        mavenCentral()
        maven { url = "https://repo1.maven.org/maven2" }
        maven { url = "https://maven-central.storage-download.googleapis.com/maven2/" }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
        testImplementation 'org.mockito:mockito-core:5.11.0'
        testImplementation 'org.mockito:mockito-junit-jupiter:5.11.0'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.withType(Javadoc).configureEach {
        exclude 'io/brane/internal/**'
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

// Aggregated Javadoc
task allJavadoc(type: Javadoc) {
    description = 'Generates aggregated Javadoc for all modules'
    group = 'documentation'

    destinationDir = layout.buildDirectory.dir("docs/javadoc").get().asFile
    title = "Brane SDK ${version} API"

    // Exclude examples from API docs
    def docProjects = subprojects.findAll { it.name != 'brane-examples' }
    
    source docProjects.collect { it.sourceSets.main.allJava }
    classpath = files(docProjects.collect { it.sourceSets.main.compileClasspath })
    
    exclude 'io/brane/internal/**'
    options.addStringOption('Xdoclint:none', '-quiet')
    options.overview = file('overview.html')
    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.links 'https://docs.oracle.com/en/java/javase/21/docs/api/'
}
