plugins {
    id 'application'
}

dependencies {
    implementation project(':brane-core')
    implementation project(':brane-rpc')
    implementation project(':brane-contract')
    implementation project(':brane-primitives')
    implementation "org.slf4j:slf4j-simple:2.0.12"
}

application {
    // Allow override via -PmainClass; default to the echo example
    mainClass = (project.findProperty("mainClass")
            ?: "io.brane.examples.Main") as String
}

// Helper: prefer -P, fall back to Gradle's own -D, then to a hard-coded default
def propOrSystem = { String key, String fallback ->
    if (project.hasProperty(key)) {
        return project.property(key)
    }
    return System.getProperty(key, fallback)
}

tasks.named('run') {
    // Echo example defaults
    systemProperty 'brane.examples.rpc',
            propOrSystem('brane.examples.rpc', 'http://127.0.0.1:8545')
    systemProperty 'brane.examples.contract',
            propOrSystem('brane.examples.contract', '0x0000000000000000000000000000000000000000')

    // ERC-20 read-only example (Erc20Example)
    systemProperty 'brane.examples.erc20.rpc',
            propOrSystem('brane.examples.erc20.rpc', null)
    systemProperty 'brane.examples.erc20.contract',
            propOrSystem('brane.examples.erc20.contract', null)
    systemProperty 'brane.examples.erc20.holder',
            propOrSystem('brane.examples.erc20.holder', null)

    // ERC-20 transfer example (Erc20TransferExample)
    systemProperty 'brane.examples.erc20.recipient',
            propOrSystem('brane.examples.erc20.recipient', null)
    systemProperty 'brane.examples.erc20.pk',
            propOrSystem('brane.examples.erc20.pk', null)
    systemProperty 'brane.examples.erc20.amount',
            propOrSystem('brane.examples.erc20.amount', null)
    systemProperty 'brane.examples.erc20.fromBlock',
            propOrSystem('brane.examples.erc20.fromBlock', null)
    systemProperty 'brane.examples.erc20.toBlock',
            propOrSystem('brane.examples.erc20.toBlock', null)

    // Multi-chain example: Base Sepolia override
    systemProperty 'brane.examples.rpc.base-sepolia',
            propOrSystem('brane.examples.rpc.base-sepolia', null)

    // Error diagnostics example
    systemProperty 'brane.examples.mode',
            propOrSystem('brane.examples.mode', 'helpers')

    // TxBuilder integration test
    systemProperty 'brane.examples.pk',
            propOrSystem('brane.examples.pk', null)

    // Revert integration test
    systemProperty 'brane.anvil.revertExample.address',
            propOrSystem('brane.anvil.revertExample.address', null)
}

task runInfuraTest(type: JavaExec) {
    group = "Execution"
    description = "Run Infura WebSocket Test"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = "io.brane.examples.InfuraWebSocketTest"

    // Load .env file manually since we don't have a dotenv plugin configured
    doFirst {
        def envFile = file("$rootDir/.env")
        if (envFile.exists()) {
            envFile.eachLine { line ->
                def trimmedLine = line.trim()
                if (trimmedLine && !trimmedLine.startsWith("#")) {
                    def parts = trimmedLine.split("=", 2)
                    if (parts.length == 2) {
                        environment parts[0].trim(), parts[1].trim()
                    }
                }
            }
        } else {
            println "Warning: .env file not found at $envFile"
        }
    }
}
