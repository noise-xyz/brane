plugins {
    id 'java'
    id 'me.champeau.jmh' version '0.7.2'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':brane-rpc')
    implementation project(':brane-core')
    implementation project(':brane-contract')
    implementation project(':brane-primitives')
    implementation 'org.web3j:core:4.10.3'
    implementation 'org.openjdk.jmh:jmh-core:1.37'
    annotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
}

jmh {
    warmupIterations = project.hasProperty('jmh.wi') ? project.property('jmh.wi') as int : 1
    iterations = project.hasProperty('jmh.i') ? project.property('jmh.i') as int : 3
    fork = project.hasProperty('jmh.f') ? project.property('jmh.f') as int : 1
    failOnError = true
    resultFormat = 'JSON'
    // Benchmarks are specified via command line: -Pjmh.includes="..."
    // Leave empty to run all benchmarks by default
    includes = project.hasProperty('jmh.includes') ? [project.property('jmh.includes')] : []
    excludes = []
    // Profilers: -Pjmh.prof="gc" for GC allocation profiler
    profilers = project.hasProperty('jmh.prof') ? [project.property('jmh.prof')] : []

    // Pass the system property to the forked JVM
    if (System.getProperty('brane.benchmark.useInfura') != null) {
        jvmArgsAppend = ["-Dbrane.benchmark.useInfura=" + System.getProperty('brane.benchmark.useInfura')]
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add("-nowarn")
    options.compilerArgs.add("-Xlint:none")
}

task runManualBenchmark(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'sh.brane.benchmark.ManualBenchmark'
}

task runRealWorldBenchmark(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'sh.brane.benchmark.RealWorldLatencyTest'
}

task runWebSocketBenchmark(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'sh.brane.benchmark.ManualWebSocketBenchmark'
}

task runQuickBenchmark(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'sh.brane.benchmark.QuickProviderBenchmark'
}

